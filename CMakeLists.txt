cmake_minimum_required(VERSION 3.16)

project(Phraims VERSION 0.55 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTORCC ON)

find_package(Qt6 6.9 COMPONENTS Widgets WebEngineWidgets REQUIRED)

# Generate version.h from version.h.in with current project version
configure_file(
  "${CMAKE_CURRENT_SOURCE_DIR}/version.h.in"
  "${CMAKE_CURRENT_BINARY_DIR}/version.h"
  @ONLY
)

# On macOS build a GUI app bundle (prevents launching a Terminal).
if(APPLE)
  #
  # From https://doc.qt.io/qt-6/appicon.html#using-cmake-on-macos
  #
  # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
  # generated by CMake. This variable contains the .icns file name,
  # without the path.
  set(MACOSX_BUNDLE_ICON_FILE phraims.icns)

  # And the following tells CMake where to find and install the file itself.
  set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/phraims.icns")
  set_source_files_properties(${app_icon_macos} PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")

  set(MACOSX_BUNDLE_EXECUTABLE_NAME Phraims)
  set(MACOSX_BUNDLE_ICON_FILE phraims.icns)
  set(MACOSX_BUNDLE_BUNDLE_NAME "${PROJECT_NAME}")
  set(MACOSX_BUNDLE_SHORT_VERSION_STRING "${PROJECT_VERSION}")
  set(MACOSX_BUNDLE_BUNDLE_VERSION "${PROJECT_VERSION}")
  set(CMAKE_OSX_DEPLOYMENT_TARGET "${CMAKE_OSX_DEPLOYMENT_TARGET}")

  configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/resources/Info.plist.in"
    "${CMAKE_CURRENT_BINARY_DIR}/Info.plist"
    @ONLY
  )

  qt_add_executable(Phraims
    MACOSX_BUNDLE
    main.cpp
    AppSettings.h
    Utils.h
    Utils.cpp
    EscapeFilter.h
    MyWebEnginePage.h
    MyWebEngineView.h
    SplitterDoubleClickFilter.h
    DomPatch.h
    DomPatch.cpp
    SplitFrameWidget.h
    SplitFrameWidget.cpp
    SplitWindow.h
    SplitWindow.cpp
    resources/resources.qrc
    ${app_icon_macos}
  )
  set_target_properties(Phraims PROPERTIES
    MACOSX_BUNDLE TRUE
    MACOSX_BUNDLE_INFO_PLIST "${CMAKE_CURRENT_BINARY_DIR}/Info.plist"
    MACOSX_BUNDLE_SHORT_VERSION_STRING "${PROJECT_VERSION}"
    MACOSX_BUNDLE_BUNDLE_VERSION "${PROJECT_VERSION}"
    MACOSX_BUNDLE_BUNDLE_NAME "${PROJECT_NAME}"
  )

elseif(WIN32)
  # On Windows prefer a GUI subsystem build to avoid a console window.
  # This sets the WIN32_EXECUTABLE property so the linker uses the
  # WINDOWS subsystem instead of CONSOLE.
  
  # Configure the Windows resource file with version information
  # Extract version components for the .rc file
  # Note: This expects semantic versioning (MAJOR.MINOR[.PATCH]) without pre-release
  # or build metadata (e.g., "1.2.3" not "1.2.3-alpha" or "1.2.3+build")
  string(REGEX MATCH "^([0-9]+)\\.([0-9]+)(\\.([0-9]+))?" _ "${PROJECT_VERSION}")
  set(PHRAIMS_VERSION_MAJOR "${CMAKE_MATCH_1}")
  set(PHRAIMS_VERSION_MINOR "${CMAKE_MATCH_2}")
  set(PHRAIMS_VERSION_PATCH "${CMAKE_MATCH_4}")
  if(NOT PHRAIMS_VERSION_PATCH)
    set(PHRAIMS_VERSION_PATCH "0")
  endif()
  
  # Copy the Windows icon to the build directory so the resource compiler can find it
  configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/resources/phraims.ico"
    "${CMAKE_CURRENT_BINARY_DIR}/phraims.ico"
    COPYONLY
  )
  
  configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/resources/phraims.rc.in"
    "${CMAKE_CURRENT_BINARY_DIR}/phraims.rc"
    @ONLY
  )
  
  qt_add_executable(Phraims
    main.cpp
    AppSettings.h
    Utils.h
    Utils.cpp
    EscapeFilter.h
    MyWebEnginePage.h
    MyWebEngineView.h
    SplitterDoubleClickFilter.h
    DomPatch.h
    DomPatch.cpp
    SplitFrameWidget.h
    SplitFrameWidget.cpp
    SplitWindow.h
    SplitWindow.cpp
    resources/resources.qrc
    "${CMAKE_CURRENT_BINARY_DIR}/phraims.rc"
  )

  # Ensure the executable is built as a GUI subsystem without a console window
  set_target_properties(Phraims PROPERTIES WIN32_EXECUTABLE TRUE)

else()
  # Linux and other platforms: standard executable build
  qt_add_executable(Phraims
    main.cpp
    AppSettings.h
    Utils.h
    Utils.cpp
    EscapeFilter.h
    MyWebEnginePage.h
    MyWebEngineView.h
    SplitterDoubleClickFilter.h
    DomPatch.h
    DomPatch.cpp
    SplitFrameWidget.h
    SplitFrameWidget.cpp
    SplitWindow.h
    SplitWindow.cpp
    resources/resources.qrc
  )

endif()

target_link_libraries(Phraims
  PRIVATE
    Qt6::Widgets
)

target_link_libraries(Phraims
  PRIVATE
    Qt6::WebEngineWidgets
)

# Add build directory to include path so generated version.h can be found
target_include_directories(Phraims PRIVATE "${CMAKE_CURRENT_BINARY_DIR}")

if(APPLE)
  set(_qt_sdk "${CMAKE_OSX_SYSROOT}")
  if(_qt_sdk AND EXISTS "${_qt_sdk}/usr/include/c++/v1")
    target_include_directories(Phraims
      SYSTEM BEFORE
      PRIVATE
        "${_qt_sdk}/usr/include/c++/v1"
    )
  endif()
endif()
